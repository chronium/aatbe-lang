use "raytracer/scene"
use "raytracer/ray"

use "../aatlib/libc"
use "../aatlib/math/funcs"

fn print color: Color = printf "%d %d %d\n", (color.red * 255.0) as u8, (color.green * 255.0) as u8, (color.blue * 255.0) as u8

fn get_color sphere: Sphere, light: Light, ray: Ray, dist: f64 -> Color = {
    val hit_point = add (to_vec3 ray.origin), mul ray.direction, dist
    val normal = normal sphere, hit_point
    val dir_to_light = mul light.direction, -1f64
    val light_pow = (max (dot normal, dir_to_light) as f32, 0.0) * light.intensity as f32
    val light_refl = sphere.albedo / PI as f32

    val color = mul (mul (mul sphere.color, light.color), light_pow), light_refl

    clamp color
}

fn get_color plane: Plane, light: Light, ray: Ray, dist: f64 -> Color = {
    val hit_point = add (to_vec3 ray.origin), mul ray.direction, dist
    val normal = normal plane, hit_point
    val dir_to_light = mul (normalized light.direction), -1f64
    val light_pow = (max (dot normal, dir_to_light) as f32, 0.0) * light.intensity as f32
    val light_refl = plane.albedo / PI as f32

    val color = mul (mul (mul plane.color, light.color), light_pow), light_refl

    clamp color
}

@entry
fn main () = {
    val scene = Scene {
        width: 1920,
        height: 1080,
        fov: 90f64,
        sphere: Sphere {
            center: Point {
                x: 0f64,
                y: 0f64,
                z: -5f64
            },
            radius: 2f64,
            color: Color {
                red: 0.2,
                green: 1.0,
                blue: 0.2
            },
            albedo: 0.18
        },
        plane: Plane {
            center: Point {
                x: 0f64,
                y: -1f64,
                z: 0f64
            },
            normal: Vec3 {
                x: 0f64,
                y: -1f64,
                z: 0f64
            },
            color: Color {
                red: 0.6,
                green: 0.8,
                blue: 1.0
            },
            albedo: 0.18
        },
        light: Light {
            direction: Vec3 {
                x: -0.25f64,
                y: -1f64,
                z: -1f64
            },
            color: Color {
                red: 1.0,
                green: 1.0,
                blue: 1.0
            },
            intensity: 20f64
        }
    }
    val black = Color {
        red: 0.0,
        green: 0.0,
        blue: 0.0
    }

    val ray = Ray {
        origin: (zero :Point),
        direction: to_vec3 (zero :Point)
    }

    printf "P3\n%d %d\n255\n", scene.width, scene.height
    var sphere_dist: f64
    var plane_dist: f64

    var y = 0
    while y < scene.height {
        var x = 0
        while x < scene.width {
            create_prime &ray, x, y, scene

            sphere_dist = intersect scene.sphere, ray
            plane_dist = intersect scene.plane, ray

            if sphere_dist != -1f64 {
                print get_color scene.sphere, scene.light, ray, sphere_dist
            } else if plane_dist != -1f64 {
                print get_color scene.plane, scene.light, ray, plane_dist
            } else {
                print black
            }

            x = x + 1
        }
        y = y + 1
    }
}
